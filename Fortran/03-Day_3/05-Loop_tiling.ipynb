{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "82d15f93",
   "metadata": {
    "editable": false
   },
   "source": [
    "# Loop tiling\n",
    "\n",
    "---\n",
    "**Requirements:**\n",
    "\n",
    "- [Get started](./Get_started.ipynb)\n",
    "- [Data Management](./Data_management.ipynb)\n",
    "---\n",
    "\n",
    "Nested loops often reuse the same data across their iterations and keeping the working set inside the caches can improve performance.\n",
    "Tiling is a partitioning method of the loops into blocks. It reorders the loops so that each block will repeatedly hit the cache.\n",
    "A first usage restriction will thus be on the loops' nature itself: not all loops can benefit from tiling, only the ones that will reuse data while showing a poor data locality, thus leading to frequent cache misses.\n",
    "\n",
    "<img alt=\"Tiles example\" src=\"../../pictures/tiles.png\" style=\"float:none\"/>\n",
    "\n",
    "OpenACC allows to improve data locality inside loops with the dedicated _tile_ clause.\n",
    "It specifies the compiler to split each loop in the nest into 2 loops, with an outer set of tile loops and an inner set of element loops."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "46b180fe",
   "metadata": {
    "editable": false
   },
   "source": [
    "## Syntax\n",
    "\n",
    "The tile clause may appear with the _loop_ directive for nested loops.\n",
    "For _N_ nested loops, the tile clause can take _N_ arguments. The first one being the size of the inner loop of the nest, the last one being the size of the outer loop.\n",
    "\n",
    "```fortran\n",
    "!$acc loop tile(32,32)\n",
    "do i = 1, size_i\n",
    "    do j = 1, size_j\n",
    "        ! A Fabulous calculation\n",
    "    enddo\n",
    "enddo\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b5b4e771",
   "metadata": {
    "editable": false
   },
   "source": [
    "## Restrictions\n",
    "\n",
    "- the tile size (corresponding to the product of the arguments of the tile clause) can be up to 1024\n",
    "- for better performance the size for the inner loop is a power of 2 (best with 32 to fit a cuda warp)\n",
    "- if the vector clause is specified, it is then applied to the element loop\n",
    "- if the gang clause is specified, it is then applied to the tile loop\n",
    "- the worker clause is applied to the element loop only if the vector clause is not specified"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "47664d5b",
   "metadata": {
    "editable": false
   },
   "source": [
    "## Example\n",
    "\n",
    "In the following example, tiling is used to solve a matrix multiplication followed by an addition. Let us take a look at the performance of the na√Øve algorithm and the manual tiling on CPU.\n",
    "\n",
    "Example stored in: `../../examples/Fortran/Loop_tiling_example_cpu.f90`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c638e1df",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%idrrun\n",
    "program tiles\n",
    "    use ISO_Fortran_env, only : INT32, REAL64\n",
    "    implicit none\n",
    "    integer(kind=INT32), parameter :: ni=4280, nj=4024, nk=1960, ntimes=30\n",
    "    real(kind=REAL64)              :: a(ni,nk), b(nk,nj), c(ni,nj), d(ni,nj)\n",
    "    real(kind=REAL64)              :: summation, t1, t2\n",
    "    integer(kind=INT32)            :: nt, i, j, k\n",
    "    integer(kind=INT32)            :: ichunk, l, ii, jj, kk   \n",
    "\n",
    "    call random_number(a)\n",
    "    call random_number(b)\n",
    "    \n",
    "    a = 4.0_real64*a - 2.0_real64\n",
    "    b = 8.0_real64*b - 4.0_real64\n",
    "    c = 2.0_real64 \n",
    "    d = 0.0_real64\n",
    "    \n",
    "    print *, \"Start calculation\"\n",
    "\n",
    "    call cpu_time(t1)\n",
    "    do nt = 1, ntimes\n",
    "      d = c + matmul(a,b)\n",
    "    end do\n",
    "    call cpu_time(t2)\n",
    "    print *, \"CPU matmul\"\n",
    "    print *, \"elapsed\",t2-t1\n",
    "\n",
    "    print *,sum(d)\n",
    "    d = 0.0_real64\n",
    "    print *, \" \"\n",
    "\n",
    "       \n",
    "    call cpu_time(t1)\n",
    "    do nt = 1, ntimes\n",
    "      do j=1,nj\n",
    "         do i =1,ni\n",
    "           summation = 0.0_real64\n",
    "           do k=1,nk\n",
    "              summation = summation + a(i,k) * b(k,j)\n",
    "           enddo\n",
    "           d(i,j) = summation + c(i,j)\n",
    "         enddo\n",
    "      enddo\n",
    "    enddo\n",
    "    call cpu_time(t2)\n",
    "    print *, \"CPU naive loop\"\n",
    "    print *, \"elapsed\",t2-t1\n",
    "    print *,sum(d)\n",
    "    d = 0.0_real64\n",
    "    print *, \" \"    \n",
    "\n",
    "\n",
    "    call cpu_time(t1)    \n",
    "    l=size(a,dim=2)\n",
    "    ichunk = 256\n",
    "    do nt = 1, ntimes\n",
    "    d = 0.0_real64\n",
    "    do jj = 1, nj, ichunk\n",
    "        do kk = 1, l, ichunk\n",
    "           do j=jj,min(jj+ichunk-1,nj)\n",
    "             do k=kk,min(kk+ichunk-1,l)\n",
    "              do i=1,ni\n",
    "                d(i,j) = d(i,j) + a(i,k) * b(k,j)\n",
    "              enddo\n",
    "             enddo\n",
    "            enddo\n",
    "         enddo\n",
    "    enddo\n",
    "    do j = 1, nj\n",
    "        do i = 1, ni\n",
    "            d(i,j) = d(i,j) +c(i,j)\n",
    "        enddo\n",
    "    enddo\n",
    "    enddo\n",
    "    call cpu_time(t2)\n",
    "    print *, \"CPU manual loop tiling 256\"\n",
    "    print *, \"elapsed\",t2-t1\n",
    "    print *,sum(d)\n",
    "    d = 0.0_real64\n",
    "    print *, \" \"\n",
    "\n",
    "    \n",
    "    call cpu_time(t1)\n",
    "    l=size(a,dim=2)\n",
    "    ichunk = 512\n",
    "    do nt = 1, ntimes\n",
    "    d = 0.0_real64\n",
    "    do jj = 1, nj, ichunk\n",
    "        do kk = 1, l, ichunk\n",
    "           do j=jj,min(jj+ichunk-1,nj)\n",
    "             do k=kk,min(kk+ichunk-1,l)\n",
    "              do i=1,ni\n",
    "                d(i,j) = d(i,j) + a(i,k) * b(k,j)\n",
    "              enddo\n",
    "             enddo\n",
    "            enddo\n",
    "         enddo\n",
    "    enddo\n",
    "    do j = 1, nj\n",
    "        do i = 1, ni\n",
    "            d(i,j) = d(i,j) +c(i,j)\n",
    "        enddo\n",
    "    enddo\n",
    "    enddo\n",
    "    call cpu_time(t2)\n",
    "    print *, \"CPU manual loop tiling 512\"\n",
    "    print *, \"elapsed\",t2-t1\n",
    "    print *,sum(d)\n",
    "    d = 0.0_real64\n",
    "\n",
    "end program tiles\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "dfb511a3",
   "metadata": {},
   "source": [
    "And now it's GPU implementation.\n",
    "\n",
    "Example stored in: `../../examples/Fortran/Loop_tiling_example_gpu.f90`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ed916e00",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%idrrun -a\n",
    "program tiles\n",
    "    use ISO_Fortran_env, only : INT32, REAL64\n",
    "    implicit none\n",
    "    integer(kind=INT32), parameter :: ni=4280, nj=4024, nk=1960, ntimes=30\n",
    "    real(kind=REAL64)              :: a(ni,nk), b(nk,nj), c(ni,nj), d(ni,nj)\n",
    "    real(kind=REAL64)              :: summation, t1, t2\n",
    "    integer(kind=INT32)            :: nt, i, j, k\n",
    "    integer(kind=INT32)            :: ichunk, l, ii, jj, kk   \n",
    "\n",
    "    call random_number(a)\n",
    "    call random_number(b)\n",
    "    \n",
    "    a = 4.0_real64*a - 2.0_real64\n",
    "    b = 8.0_real64*b - 4.0_real64\n",
    "    c = 2.0_real64 \n",
    "    d = 0.0_real64\n",
    "    \n",
    "    print *, \"Start calculation\"\n",
    "\n",
    "    !$acc enter data copyin(a,b,c) create(d)\n",
    "\n",
    "    call cpu_time(t1)\n",
    "    l=size(a,dim=2)\n",
    "    ichunk = 256\n",
    "    do nt = 1, ntimes\n",
    "    !$acc parallel loop default(present)\n",
    "    do j = 1, nj\n",
    "        !$acc loop\n",
    "        do i = 1, ni\n",
    "            d(i,j) = 0.0_real64\n",
    "        enddo\n",
    "    enddo\n",
    "\n",
    "    !$acc parallel loop default(present)\n",
    "    do jj = 1, nj, ichunk\n",
    "        do kk = 1, l, ichunk\n",
    "           do j=jj,min(jj+ichunk-1,nj)\n",
    "             do k=kk,min(kk+ichunk-1,l)\n",
    "              !$acc loop vector\n",
    "              do i=1,ni\n",
    "                d(i,j) = d(i,j) + a(i,k) * b(k,j)\n",
    "              enddo\n",
    "             enddo\n",
    "            enddo\n",
    "         enddo\n",
    "    enddo\n",
    "   !$acc parallel loop default(present)\n",
    "    do j = 1, nj\n",
    "        !$acc loop\n",
    "        do i = 1, ni\n",
    "            d(i,j) = d(i,j) +c(i,j)\n",
    "        enddo\n",
    "    enddo\n",
    "    enddo    \n",
    "    call cpu_time(t2)\n",
    "    print *, \"GPU manual loop tiling 256\"\n",
    "    print *, \"elapsed\",t2-t1\n",
    "    !$acc update self(d(:,:))\n",
    "    print *,sum(d)\n",
    "    !$acc kernels\n",
    "    d(:,:) = 0.0_real64\n",
    "    !$acc end kernels\n",
    "    print *, \" \"\n",
    "\n",
    "\n",
    "    call cpu_time(t1)\n",
    "    l=size(a,dim=2)\n",
    "    ichunk = 512 \n",
    "    do nt = 1, ntimes\n",
    "    !$acc parallel loop default(present)\n",
    "    do j = 1, nj\n",
    "        !$acc loop\n",
    "        do i = 1, ni\n",
    "            d(i,j) = 0.0_real64\n",
    "        enddo\n",
    "    enddo\n",
    "\n",
    "    !$acc parallel loop gang default(present)\n",
    "    do jj = 1, nj, ichunk\n",
    "        !$acc loop seq\n",
    "        do kk = 1, l, ichunk\n",
    "           !$acc loop seq\n",
    "           do j=jj,min(jj+ichunk-1,nj)\n",
    "             do k=kk,min(kk+ichunk-1,l)\n",
    "              !$acc loop vector\n",
    "              do i=1,ni\n",
    "                d(i,j) = d(i,j) + a(i,k) * b(k,j)\n",
    "              enddo\n",
    "             enddo\n",
    "            enddo\n",
    "         enddo\n",
    "    enddo\n",
    "    !$acc parallel loop default(present)\n",
    "    do j = 1, nj\n",
    "        !$acc loop\n",
    "        do i = 1, ni\n",
    "            d(i,j) = d(i,j) +c(i,j)\n",
    "        enddo\n",
    "    enddo\n",
    "    enddo\n",
    "    call cpu_time(t2)\n",
    "    print *, \"GPU manual loop tiling 512\"\n",
    "    print *, \"elapsed\",t2-t1\n",
    "    !$acc update self(d(:,:))\n",
    "    print *,sum(d)\n",
    "    !$acc kernels\n",
    "    d(:,:) = 0.0_real64\n",
    "    !$acc end kernels\n",
    "    print *, \" \"\n",
    "\n",
    "    call cpu_time(t1)\n",
    "    l=size(a,dim=2)\n",
    "    ichunk = 16\n",
    "    do nt = 1, ntimes\n",
    "    !$acc parallel loop default(present)\n",
    "    do j = 1, nj\n",
    "        !$acc loop\n",
    "        do i = 1, ni\n",
    "            d(i,j) = 0.0_real64\n",
    "        enddo\n",
    "    enddo\n",
    "\n",
    "    !$acc parallel loop gang default(present)\n",
    "    do jj = 1, nj, ichunk\n",
    "        !$acc loop seq\n",
    "        do kk = 1, l, ichunk\n",
    "           !$acc loop seq\n",
    "           do j=jj,min(jj+ichunk-1,nj)\n",
    "             do k=kk,min(kk+ichunk-1,l)\n",
    "              !$acc loop vector\n",
    "              do i=1,ni\n",
    "                d(i,j) = d(i,j) + a(i,k) * b(k,j)\n",
    "              enddo\n",
    "             enddo\n",
    "            enddo\n",
    "         enddo\n",
    "    enddo\n",
    "    !$acc parallel loop default(present)\n",
    "    do j = 1, nj\n",
    "        !$acc loop\n",
    "        do i = 1, ni\n",
    "            d(i,j) = d(i,j) +c(i,j)\n",
    "        enddo\n",
    "    enddo\n",
    "    enddo\n",
    "    call cpu_time(t2)\n",
    "    print *, \"GPU manual loop tiling 16\"\n",
    "    print *, \"elapsed\",t2-t1\n",
    "    !$acc update self(d(:,:))\n",
    "    print *,sum(d)\n",
    "    !$acc kernels\n",
    "    d(:,:) = 0.0_real64\n",
    "    !$acc end kernels\n",
    "    print *, \" \"\n",
    "\n",
    "\n",
    "    call cpu_time(t1)\n",
    "    do nt = 1, ntimes\n",
    "      !$acc parallel loop default(present)\n",
    "      do j=1,nj\n",
    "         !$acc loop\n",
    "         do i =1,ni\n",
    "           summation = 0.0_real64\n",
    "           !$acc loop seq\n",
    "           do k=1,nk\n",
    "              summation = summation + a(i,k) * b(k,j)\n",
    "           enddo\n",
    "           d(i,j) = summation + c(i,j)\n",
    "         enddo\n",
    "      enddo\n",
    "    enddo\n",
    "\n",
    "    call cpu_time(t2)\n",
    "    print *, \"GPU naive parallel loop\"\n",
    "    print *, \"elapsed\",t2-t1\n",
    "\n",
    "    !$acc update self(d(:,:))\n",
    "    print *,sum(d)\n",
    "    print *, \" \"\n",
    "    d(:,:) = 0.0_real64\n",
    "\n",
    "    call cpu_time(t1)\n",
    "    do nt = 1, ntimes\n",
    "      !$acc parallel loop tile(32,32) default(present)\n",
    "      do j=1,nj\n",
    "         do i=1,ni\n",
    "           summation = 0.0_real64\n",
    "           !$acc loop seq\n",
    "           do k=1,nk\n",
    "              summation = summation + a(i,k) * b(k,j)\n",
    "           enddo\n",
    "           d(i,j) = summation + c(i,j)\n",
    "         enddo\n",
    "      enddo\n",
    "    enddo\n",
    "\n",
    "    call cpu_time(t2)\n",
    "    print *, \"GPU naive parallel loop tiled\"\n",
    "    print *, \"elapsed\",t2-t1\n",
    "\n",
    "    !$acc exit data copyout(d(:,:))\n",
    "\n",
    "    print *,sum(d)\n",
    "end program tiles\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "38e03882",
   "metadata": {
    "editable": false
   },
   "source": [
    "## Exercise\n",
    "\n",
    "In this exercise, you will try to accelerate the numerical resolution of the 2D Laplace's equation with tiles. You can see that tiles parameter should be chosen wisely in order not to deteriorate performance.\n",
    "\n",
    "Example stored in: `../../examples/Fortran/Loop_tiling_exercise.f90`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c92a7730",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%idrrun -a\n",
    "program laplace2d \n",
    "use ISO_FORTRAN_ENV, only : real64, int32\n",
    "!use openacc\n",
    "implicit none\n",
    "\n",
    "! Calculated solution for (E,B) fields\n",
    "real   (kind=real64), dimension(:,:), allocatable :: T, T_new\n",
    "! Dimension of the system\n",
    "integer(kind=int32 )                              :: nx, ny ! number of points\n",
    "\n",
    "integer(kind=int32 )                              :: i, j, it\n",
    "real   (kind=real64)                              :: erreur\n",
    "\n",
    "nx = 20000 !30000\n",
    "ny = 10000 !30000\n",
    "\n",
    "allocate(T(nx,ny),T_new(nx,ny))\n",
    "\n",
    "! initial conditions\n",
    "do j=2,ny-1\n",
    "    do i=2,nx-1\n",
    "        T(i,j)     = 0.0_real64\n",
    "        T_new(i,j) = 0.0_real64\n",
    "    enddo\n",
    "enddo\n",
    "!\n",
    "do i=1,nx\n",
    "    T(i, 1) = 100.0_real64\n",
    "    T(i,ny) = 0.0_real64\n",
    "enddo\n",
    "!\n",
    "do i=1,ny\n",
    "    T(1 ,i) = 0.0_real64\n",
    "    T(nx,i) = 0.0_real64\n",
    "enddo\n",
    "\n",
    "! add acc directive\n",
    "do it = 1, 10000\n",
    "  erreur = 0.0_real64\n",
    "  !add acc directive\n",
    "  do j= 2,ny-1\n",
    "      do i= 2,nx-1\n",
    "         T_new(i,j) =  0.25_real64*(T(i+1,j)+T(i-1,j) + &\n",
    "                                    T(i,j+1)+T(i,j-1))\n",
    "         erreur = max(erreur, abs(T_new(i,j) - T(i,j)))\n",
    "      enddo\n",
    "  enddo\n",
    "\n",
    "  if (mod(it,100) .eq. 0) print *, \"iteration: \",it,\" erreur: \",erreur\n",
    "\n",
    "  !add acc directive\n",
    "  do j= 2,ny-1\n",
    "  do i= 2,nx-1\n",
    "     T(i,j) =  T_new(i,j)\n",
    "  enddo\n",
    "  enddo  \n",
    "enddo\n",
    "\n",
    "\n",
    "deallocate(T, T_new)\n",
    "\n",
    "end program laplace2d"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f3a781cd",
   "metadata": {
    "editable": false
   },
   "source": [
    "## Solution\n",
    "\n",
    "Example stored in: `../../examples/Fortran/Loop_tiling_solution.f90`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b1055653",
   "metadata": {
    "jupyter": {
     "source_hidden": true
    }
   },
   "outputs": [],
   "source": [
    "%%idrrun -a\n",
    "program laplace2d \n",
    "use ISO_FORTRAN_ENV, only : real64, int32\n",
    "!use openacc\n",
    "implicit none\n",
    "\n",
    "! Calculated solution for (E,B) fields\n",
    "real   (kind=real64), dimension(:,:), allocatable :: T, T_new\n",
    "! Dimension of the system\n",
    "integer(kind=int32 )                              :: nx, ny ! number of points\n",
    "\n",
    "integer(kind=int32 )                              :: i, j, it\n",
    "real   (kind=real64)                              :: erreur\n",
    "\n",
    "nx = 20000 !30000\n",
    "ny = 10000 !30000\n",
    "\n",
    "allocate(T(nx,ny),T_new(nx,ny))\n",
    "\n",
    "! initial conditions\n",
    "do j=2,ny-1\n",
    "    do i=2,nx-1\n",
    "        T(i,j)     = 0.0_real64\n",
    "        T_new(i,j) = 0.0_real64\n",
    "    enddo\n",
    "enddo\n",
    "!\n",
    "do i=1,nx\n",
    "    T(i, 1) = 100.0_real64\n",
    "    T(i,ny) = 0.0_real64\n",
    "enddo\n",
    "!\n",
    "do i=1,ny\n",
    "    T(1 ,i) = 0.0_real64\n",
    "    T(nx,i) = 0.0_real64\n",
    "enddo\n",
    "\n",
    "!$acc data copy(T) create(T_new)\n",
    "do it = 1, 10000\n",
    "  erreur = 0.0_real64\n",
    "  !$acc parallel loop tile(32,32) reduction(max:erreur)\n",
    "  do j= 2,ny-1\n",
    "      do i= 2,nx-1\n",
    "         T_new(i,j) =  0.25_real64*(T(i+1,j)+T(i-1,j) + &\n",
    "                                    T(i,j+1)+T(i,j-1))\n",
    "         erreur = max(erreur, abs(T_new(i,j) - T(i,j)))\n",
    "      enddo\n",
    "  enddo\n",
    "\n",
    "  if (mod(it,100) .eq. 0) print *, \"iteration: \",it,\" erreur: \",erreur\n",
    "\n",
    "  !$acc parallel loop tile(32,32)\n",
    "  do j= 2,ny-1\n",
    "  do i= 2,nx-1\n",
    "     T(i,j) =  T_new(i,j)\n",
    "  enddo\n",
    "  enddo  \n",
    "enddo\n",
    "!$acc end data\n",
    "\n",
    "deallocate(T, T_new)\n",
    "\n",
    "end program laplace2d"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "GPU Directives",
   "language": "python",
   "name": "gpu_directives"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
