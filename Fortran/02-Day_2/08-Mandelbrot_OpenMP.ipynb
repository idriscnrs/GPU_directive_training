{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "e9be226f",
   "metadata": {
    "editable": false
   },
   "source": [
    "# Generate Mandelbrot set\n",
    "\n",
    "---\n",
    "**Requirements:**\n",
    "\n",
    "- [Get started](./Get_started.ipynb)\n",
    "- [Data management](./Data_management.ipynb)\n",
    "- [Multi GPU](./MultiGPU.ipynb)\n",
    "\n",
    "---"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d2ccc711",
   "metadata": {
    "editable": false
   },
   "source": [
    "## Introduction\n",
    "\n",
    " The Mandelbrot set is the set of complex numbers c for which the function  $f_c(z) = z^2+c$\n",
    "does not diverge when iterated from z = 0. [Wikipedia](https://en.wikipedia.org/wiki/Mandelbrot_set)\n",
    "\n",
    "<img align=\"center\" src=\"../../pictures/mandelbrot.jpg\" style=\"float:none\" width=\"500\"/>\n",
    "\n",
    "By Created by <a href=\"//commons.wikimedia.org/wiki/User:Wolfgangbeyer\" title=\"User:Wolfgangbeyer\">Wolfgang Beyer</a> with the program Ultra Fractal 3. - <span class=\"int-own-work\" lang=\"en\">Own work</span>, <a href=\"http://creativecommons.org/licenses/by-sa/3.0/\" title=\"Creative Commons Attribution-Share Alike 3.0\">CC BY-SA 3.0</a>, <a href=\"https://commons.wikimedia.org/w/index.php?curid=321973\">Link</a>\n",
    "\n",
    "In this hands-on you will generate a picture with the Mandelbrot set using a Multi-GPU version of the code.\n",
    "We use the OpenMP language to split the work between the GPUs."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1f43888e",
   "metadata": {
    "editable": false
   },
   "source": [
    "## What to do\n",
    "\n",
    "Add the directives to use several GPUs. Here we do __not__ need the GPU to communicate.\n",
    "Be careful to allocate the memory only for the part of the picture treated by the GPU and not the complete memory.\n",
    "\n",
    "The default coordinates show the well known representation of the set.\n",
    "If you want to play around have a look at [this webpage](http://paulbourke.net/fractals/mandelbrot/) giving interesting areas of the set on which you can \"zoom\".\n",
    "\n",
    "Example stored in: `../../examples/Fortran/mandelbrot_openmp_exercise.f90`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "08861322",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%idrrun --cliopts \"8000 4000\" -t -g 4 --threads 4 --get mandel.gray\n",
    "! you should add ` --option \"-cpp\" ` as argument to the idrrun command\n",
    "program mandelbrot\n",
    "    #ifdef _OPENMP\n",
    "    use OMP_LIB\n",
    "    #endif\n",
    "    #ifdef _OPENACC \n",
    "    use openacc\n",
    "    #endif\n",
    "    implicit none\n",
    "    integer, parameter               :: max_iter = 127\n",
    "    real, parameter                  :: min_re = -2.0\n",
    "    real, parameter                  :: max_re = 1.0\n",
    "    real, parameter                  :: min_im = -1.0\n",
    "    real, parameter                  :: max_im = 1.0\n",
    "    integer                          :: rank\n",
    "    integer                          :: first,last,width,height\n",
    "    integer                          :: num_elements,num_threads,num_gpu\n",
    "    real                             :: step_w,step_h\n",
    "    character(len=:), allocatable    :: arg1,arg2\n",
    "    integer (kind=1), allocatable    :: picture(:)\n",
    "    real                             :: x,y\n",
    "    integer                          :: len1,len2\n",
    "    integer                          :: i,j,first_elem,last_elem\n",
    "    #ifdef _OPENACC\n",
    "    integer(acc_device_kind)         :: type_d\n",
    "    #endif\n",
    "\n",
    "    call GET_COMMAND_ARGUMENT(1,LENGTH=len1)\n",
    "    allocate(character(len=len1) :: arg1)\n",
    "    call GET_COMMAND_ARGUMENT(1,VALUE=arg1)\n",
    "    call GET_COMMAND_ARGUMENT(2,LENGTH=len2)\n",
    "    allocate(character(len=len2) :: arg2)\n",
    "    call GET_COMMAND_ARGUMENT(2,VALUE=arg2)\n",
    "    read(arg1,'(i10)') width\n",
    "    read(arg2,'(i10)') height\n",
    "    step_w = 1.0 / width\n",
    "    step_h = 1.0 / height\n",
    "\n",
    "    deallocate(arg1,arg2)\n",
    "\n",
    "    allocate(picture(0:width*height-1))\n",
    "\n",
    "\n",
    "    !$OMP parallel private(first, last, rank, num_threads, num_elements, num_gpu, x, y, i, j, last_elem, first_elem) shared(picture) firstprivate(height, width, step_h, step_w) default(none)\n",
    "\n",
    "    #ifdef _OPENMP\n",
    "     rank         = OMP_GET_THREAD_NUM()\n",
    "     num_threads  = OMP_GET_NUM_THREADS()\n",
    "     first        =  rank * (height/num_threads)\n",
    "     last         = (rank + 1) * (height/num_threads) -1\n",
    "     num_elements = width*height/num_threads\n",
    "    #endif\n",
    "\n",
    "    print *, \"Using OpenMP\"\n",
    "\n",
    "    write(unit=*,fmt=\"(a9,i3,a18,i8,a3,i8,a5,i10,a9)\") \"I am rank\",rank, &\n",
    "          \" and my range is [\",first,\" ,\",last,\"[ ie \",num_elements,\" elements\"\n",
    "\n",
    "    #ifdef _OPENACC\n",
    "     type_d  = acc_get_device_type()\n",
    "     num_gpu = acc_get_num_devices(type_d)\n",
    "     call acc_set_device_num(mod(rank,num_gpu), type_d)\n",
    "     first_elem   = first*width\n",
    "     last_elem    = first_elem + num_elements-1\n",
    "     print *, \"I am rank\",rank,\". I am using GPU #\", &\n",
    "              acc_get_device_num(type_d),first_elem,last_elem\n",
    "    #endif\n",
    "\n",
    "    do i = first, last\n",
    "         do j = 0, width-1\n",
    "             x =  min_re + j * step_w * (max_re - min_re)\n",
    "             y =  min_im + i * step_h * (max_im - min_im)\n",
    "             picture(i*width+j) = mandelbrot_iterations(x,y)\n",
    "         enddo\n",
    "    enddo\n",
    "\n",
    "    !$OMP end parallel\n",
    "\n",
    "\n",
    "    open(unit=10, FILE=\"mandel.gray\", ACCESS=\"stream\", FORM=\"unformatted\")\n",
    "        write(unit=10,pos=1) picture \n",
    "    close(10)\n",
    "\n",
    "    deallocate(picture)\n",
    "    contains\n",
    "    integer(kind=1) function mandelbrot_iterations(x,y)\n",
    "        !$acc routine seq\n",
    "        real, intent(in) :: x,y\n",
    "        real             :: z1,z2,z1_old,z2_old\n",
    "        z1 = 0.0\n",
    "        z2 = 0.0\n",
    "        mandelbrot_iterations = 0\n",
    "        do while (((z1*z1+z2*z2) .le. 4) .and. (mandelbrot_iterations .lt. max_iter))\n",
    "            z1_old = z1\n",
    "            z2_old = z2\n",
    "            z1 = z1_old*z1_old - z2_old*z2_old  + x\n",
    "            z2 = 2.0*z1_old*z2_old      + y\n",
    "            mandelbrot_iterations = mandelbrot_iterations + 1\n",
    "        enddo\n",
    "    end function mandelbrot_iterations\n",
    "end program mandelbrot"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1400e0a6",
   "metadata": {},
   "outputs": [],
   "source": [
    "from idrcomp import show_gray\n",
    "show_gray(\"mandel.gray\", 8000, 4000)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0af8fe14",
   "metadata": {
    "editable": false
   },
   "source": [
    "## Solution\n",
    "\n",
    "Example stored in: `../../examples/Fortran/mandelbrot_openmp_solution.f90`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6b5a54f2",
   "metadata": {
    "jupyter": {
     "source_hidden": true
    }
   },
   "outputs": [],
   "source": [
    "%%idrrun --cliopts \"8000 4000\" -t -a -g 4\n",
    "! you should add ` --option \"-cpp -Minline\" ` as argument to the idrrun command\n",
    "program mandelbrot\n",
    "    #ifdef _OPENMP\n",
    "    use OMP_LIB\n",
    "    #endif\n",
    "    #ifdef _OPENACC \n",
    "    use openacc\n",
    "    #endif\n",
    "    implicit none\n",
    "    integer, parameter               :: max_iter = 127\n",
    "    !$acc declare create(max_iter)\n",
    "    real, parameter                  :: min_re = -2.0\n",
    "    real, parameter                  :: max_re = 1.0\n",
    "    real, parameter                  :: min_im = -1.0\n",
    "    real, parameter                  :: max_im = 1.0\n",
    "    !$acc declare create(min_re,max_re,min_im,max_im)\n",
    "    integer                          :: rank\n",
    "    integer                          :: first,last,width,height\n",
    "    integer                          :: num_elements,num_threads,num_gpu\n",
    "    real                             :: step_w,step_h\n",
    "    character(len=:), allocatable    :: arg1,arg2\n",
    "    integer (kind=1), allocatable    :: picture(:)\n",
    "    real                             :: x,y\n",
    "    integer                          :: len1,len2\n",
    "    integer                          :: i,j,first_elem,last_elem\n",
    "    #ifdef _OPENACC\n",
    "    integer(acc_device_kind)         :: type_d\n",
    "    #endif\n",
    "\n",
    "    call GET_COMMAND_ARGUMENT(1,LENGTH=len1)\n",
    "    allocate(character(len=len1) :: arg1)\n",
    "    call GET_COMMAND_ARGUMENT(1,VALUE=arg1)\n",
    "    call GET_COMMAND_ARGUMENT(2,LENGTH=len2)\n",
    "    allocate(character(len=len2) :: arg2)\n",
    "    call GET_COMMAND_ARGUMENT(2,VALUE=arg2)\n",
    "    read(arg1,'(i10)') width\n",
    "    read(arg2,'(i10)') height\n",
    "    step_w = 1.0 / width\n",
    "    step_h = 1.0 / height\n",
    "\n",
    "    deallocate(arg1,arg2)\n",
    "\n",
    "    allocate(picture(0:width*height-1))\n",
    "\n",
    "    !$OMP parallel private(first, last, rank, num_threads, num_elements, num_gpu, type_d, x, y, i, j, last_elem, first_elem) shared(picture) firstprivate(height, width, step_h, step_w) default(none)\n",
    "\n",
    "    #ifdef _OPENMP\n",
    "     rank         = OMP_GET_THREAD_NUM()\n",
    "     num_threads  = OMP_GET_NUM_THREADS()\n",
    "     first        =  rank * (height/num_threads)\n",
    "     last         = (rank + 1) * (height/num_threads) -1\n",
    "     num_elements = width*height/num_threads\n",
    "    #endif\n",
    "\n",
    "    print *, \"Using OpenMP\"\n",
    "\n",
    "    write(unit=*,fmt=\"(a9,i3,a18,i8,a3,i8,a5,i10,a9)\") \"I am rank\",rank, &\n",
    "          \" and my range is [\",first,\" ,\",last,\"[ ie \",num_elements,\" elements\"\n",
    "\n",
    "    #ifdef _OPENACC\n",
    "     type_d  = acc_get_device_type()\n",
    "     num_gpu = acc_get_num_devices(type_d)\n",
    "     call acc_set_device_num(mod(rank,num_gpu), type_d)\n",
    "     first_elem   = first*width\n",
    "     last_elem    = first_elem + num_elements-1\n",
    "     print *, \"I am rank\",rank,\". I am using GPU #\", &\n",
    "              acc_get_device_num(type_d),first_elem,last_elem\n",
    "    #endif\n",
    "\n",
    "    !$acc data copyout(picture(first_elem:last_elem))\n",
    "\n",
    "    !$acc parallel loop present(picture(first_elem:last_elem)) private(x,y)\n",
    "    do i = first, last\n",
    "        !$acc loop\n",
    "         do j = 0, width-1\n",
    "             x =  min_re + j * step_w * (max_re - min_re)\n",
    "             y =  min_im + i * step_h * (max_im - min_im)\n",
    "             picture(i*width+j) = mandelbrot_iterations(x,y)\n",
    "         enddo\n",
    "    enddo\n",
    "    !$acc end data\n",
    "\n",
    "    !$OMP end parallel\n",
    "\n",
    "    open(unit=10, FILE=\"mandel.gray\", ACCESS=\"stream\", FORM=\"unformatted\")\n",
    "        write(unit=10,pos=1) picture \n",
    "    close(10)\n",
    "\n",
    "    deallocate(picture)\n",
    "    contains\n",
    "    integer(kind=1) function mandelbrot_iterations(x,y)\n",
    "        !$acc routine seq\n",
    "        real, intent(in) :: x,y\n",
    "        real             :: z1,z2,z1_old,z2_old\n",
    "        z1 = 0.0\n",
    "        z2 = 0.0\n",
    "        mandelbrot_iterations = 0\n",
    "        do while (((z1*z1+z2*z2) .le. 4) .and. (mandelbrot_iterations .lt. max_iter))\n",
    "            z1_old = z1\n",
    "            z2_old = z2\n",
    "            z1 = z1_old*z1_old - z2_old*z2_old  + x\n",
    "            z2 = 2.0*z1_old*z2_old      + y\n",
    "            mandelbrot_iterations = mandelbrot_iterations + 1\n",
    "        enddo\n",
    "    end function mandelbrot_iterations\n",
    "end program mandelbrot"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c17c57e4",
   "metadata": {},
   "outputs": [],
   "source": [
    "from idrcomp import show_gray\n",
    "show_gray(\"mandel.gray\", 8000, 4000)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "GPU Directives",
   "language": "python",
   "name": "gpu_directives"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
