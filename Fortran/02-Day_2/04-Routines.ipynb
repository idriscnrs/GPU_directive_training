{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "6be01e17",
   "metadata": {
    "editable": false
   },
   "source": [
    "# Using OpenACC in modular programming\n",
    "\n",
    "---\n",
    "**Requirements:**\n",
    "\n",
    "- [Get started](./Get_started.ipynb)\n",
    "- [Data management](./Data_management.ipynb)\n",
    "- [Loop configuration](./Loop_configuration.ipynb)\n",
    "\n",
    "---\n",
    "\n",
    "Most modern codes use modular programming to make the readability and maintenance easier.\n",
    "You will have to deal with it inside your own code and be careful to make all functions accessible where you need.\n",
    "\n",
    "If you call a function inside a kernel, then you need to tell the compiler to create a version for the GPU.\n",
    "With OpenACC you have to use the `acc routine` directive for this purpose.\n",
    "\n",
    "With Fortran you will have to take care of the variables that are declared inside modules and use `acc declare create`."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2bfdf629",
   "metadata": {
    "editable": false
   },
   "source": [
    "## `acc routine <max_level_of_parallelism>`\n",
    "\n",
    "This directive is used to tell the compiler to create a function for the GPU as well as for the CPU.\n",
    "Since the function is available for the GPU you will be able to call it inside a kernel.\n",
    "\n",
    "When you use this directive you sign a contract with the compiler (normally no soul selling, but check it twice!) \n",
    "and promise that the function will be called inside a section of code for which work sharing at this level is not yet activated.\n",
    "The clauses available are:\n",
    "\n",
    "- gang\n",
    "- worker\n",
    "- vector\n",
    "- seq: the function is executed sequentially by one GPU thread\n",
    "\n",
    "The directive is added before the function definition or declaration:\n",
    "```fortran\n",
    "subroutine mean_value(array, array_size)\n",
    "!$acc routine seq\n",
    "    ! compute the mean value\n",
    "end subroutine mean_value\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9618b8d6",
   "metadata": {
    "editable": false
   },
   "source": [
    "### Wrong examples\n",
    "\n",
    "Since it might be a bit tricky here are some wrong examples with an explanation:\n",
    "This example is wrong because `acc parallel loop worker` activates work sharing at the _worker_ level of parallelism.\n",
    "The `acc routine worker` indicates that the function can activate _worker_ and _vector_ level of parallelism and you cannot activate twice the same level.\n",
    "```fortran\n",
    "subroutine my_worker_subroutine\n",
    "    !$acc routine worker\n",
    "    ...\n",
    "end subroutine\n",
    "\n",
    "...\n",
    "!$acc parallel loop worker\n",
    "do i=1, sys_size\n",
    "    call my_worker_subroutine\n",
    "done\n",
    "```\n",
    "\n",
    "For a similar reason this is forbidden:\n",
    "```fortran\n",
    "subroutine my_gang_subroutine\n",
    "    $!acc routine gang\n",
    "    ...\n",
    "end subroutine\n",
    "\n",
    "...\n",
    "!$acc parallel\n",
    "!$acc loop gang\n",
    "do i=1, sys_size\n",
    "    call my_gang_subroutine\n",
    "enddo\n",
    "\n",
    "!$acc end parallel\n",
    "```\n",
    "\n",
    "This example is wrong since it breaks the promise you make to the compiler.\n",
    "A vector routine cannot have loops at the _gang_ and _worker_ levels of parallelism.\n",
    "```fortran\n",
    "subroutine my_wrong_subroutine\n",
    "!$acc routine vector\n",
    "    ...\n",
    "    !$acc loop gang worker\n",
    "    do i=0, sys_size\n",
    "        // some loop stuff\n",
    "    enddo\n",
    "end subroutine\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f7e45bfb",
   "metadata": {
    "editable": false
   },
   "source": [
    "## Named `acc routine(name) <max_level_of_parallelism>`\n",
    "\n",
    "You can declare the `acc routine` directive anywhere a function prototype is allowed within the specification part of the routine.\n",
    "\n",
    "```fortran\n",
    "module utils\n",
    "    use openacc\n",
    "    interface beautiful\n",
    "        module procedure beautiful_name\n",
    "        !$acc routine(beautiful_name) vector\n",
    "    end interface\n",
    "    contains\n",
    "       subroutine beautiful_name(name)\n",
    "       ! Do something\n",
    "       end subroutine beautiful_name\n",
    "\n",
    "end module utils\n",
    "\n",
    "program another_brick\n",
    "    use utils\n",
    "    ...\n",
    "    !$acc routine(beautiful_name) vector\n",
    "    ...\n",
    "    call beautiful(name)\n",
    "    ! integers are beauty\n",
    "end program another_brick\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "672e83e3",
   "metadata": {
    "editable": false
   },
   "source": [
    "## Directives inside an `acc routine`\n",
    "\n",
    "Routines you declare with `acc routine` shall not contain directives to create kernels (_parallel_, _serial_, _kernels_).\n",
    "You have to consider the content of the function already inside a kernel.\n",
    "\n",
    "```fortran\n",
    "subroutine init(array, array_size)\n",
    "!$acc routine vector\n",
    "integer, dimension(:), intent(inout) :: array\n",
    "integer, intent(in)                  :: array_size\n",
    "integer                              :: i\n",
    "    !$acc loop\n",
    "    do i = 1, array_size\n",
    "        array(i) = i\n",
    "    enddo\n",
    "end subroutine init\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a54220e5",
   "metadata": {
    "editable": false
   },
   "source": [
    "## Exercise\n",
    "\n",
    "In this exercise, you have to compute the mean value of each row of a matrix.\n",
    "The value is computed by a function `mean_value` working on one row at a time.\n",
    "This function can use parallelism.\n",
    "\n",
    "To have correct results, you will need to make the variable `local_mean` private for each thread.\n",
    "To achieve this you have to use the _private(vars, ...)_ clause of the `acc loop` directive.\n",
    "\n",
    "Example stored in: `../../examples/Fortran/Modular_programming_mean_value_exercise.f90`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "33ba114c",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%idrrun -a\n",
    "module calcul\n",
    "    use iso_fortran_env, only : INT32, REAL64        \n",
    "    contains\n",
    "        subroutine rand_init(array,n)\n",
    "            real   (kind=REAL64), dimension(1,n), intent(inout) :: array\n",
    "            integer(kind=INT32 ), intent(in)                    :: n\n",
    "            real   (kind=REAL64)                                :: rand_val\n",
    "            integer(kind=INT32)                                 :: i\n",
    "\n",
    "            call srand(12345900)\n",
    "            do i = 1, n\n",
    "               call random_number(rand_val)\n",
    "               array(1,i) = 2.0_real64*(rand_val-0.5_real64)\n",
    "            enddo\n",
    "\n",
    "        end subroutine rand_init\n",
    "\n",
    "        subroutine iterate(array, array_size, cell_size)\n",
    "            real   (kind=REAL64), dimension(array_size,1), intent(inout) :: array\n",
    "            integer(kind=INT32 ), intent(in)                             :: array_size, cell_size\n",
    "            real   (kind=REAL64)                                         :: local_mean\n",
    "            integer(kind=INT32 )                                         :: i\n",
    "\n",
    "            do i = cell_size/2, array_size-cell_size/2\n",
    "                local_mean = mean_value(array(i+1-cell_size/2:i+cell_size/2,1), cell_size)\n",
    "                if (local_mean .lt. 0.0_real64) then\n",
    "                    array(i,1) = array(i,1) + 0.1\n",
    "                else\n",
    "                    array(i,1) = array(i,1) - 0.1\n",
    "                endif\n",
    "            enddo\n",
    "        end subroutine iterate\n",
    "\n",
    "        function mean_value(t, n)\n",
    "            real   (kind=REAL64), dimension(n,1), intent(inout) :: t\n",
    "            integer(kind=INT32 ), intent(in)                    :: n\n",
    "            real   (kind=REAL64)                                :: mean_value\n",
    "            integer(kind=INT32 )                                :: i\n",
    "            mean_value = 0.0_real64\n",
    "            do i = 1, n\n",
    "                mean_value = mean_value + t(i,1)\n",
    "            enddo\n",
    "            mean_value = mean_value / dble(n)\n",
    "        end function mean_value\n",
    "end module calcul\n",
    "\n",
    "program modular_programming\n",
    "    use calcul\n",
    "    implicit none    \n",
    "    \n",
    "    real   (kind=REAL64), dimension(:,:), allocatable :: table\n",
    "    real   (kind=REAL64), dimension(:)  , allocatable :: mean_values\n",
    "    integer(kind=INT32 )                              :: nx, ny, cell_size, i\n",
    "\n",
    "    nx =  500000\n",
    "    ny =    3000\n",
    "    allocate(table(nx,ny), mean_values(ny))\n",
    "    table(:,:) = 0.0_real64\n",
    "    call rand_init(table(1,:),ny)\n",
    "    cell_size = 32\n",
    "    do i = 2, ny   \n",
    "        call iterate(table(:,i), nx, cell_size)\n",
    "    enddo\n",
    "\n",
    "    do i = 1, ny\n",
    "        mean_values(i) = mean_value(table(:,i), nx)\n",
    "    enddo\n",
    "\n",
    "    do i = 1, 10\n",
    "        write(0,\"(a18,i5,a1,f20.8)\") \"Mean value of row \",i,\"=\",mean_values(i)\n",
    "    enddo\n",
    "\n",
    "    do i = ny-10, ny\n",
    "        write(0,\"(a18,i5,a1,f20.8)\") \"Mean value of row \",i,\"=\",mean_values(i)\n",
    "    enddo    \n",
    "    \n",
    "    deallocate(table, mean_values)\n",
    "end program modular_programming"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7a3699df",
   "metadata": {
    "editable": false
   },
   "source": [
    "### Solution\n",
    "\n",
    "Example stored in: `../../examples/Fortran/Modular_programming_mean_value_solution.f90`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "660831d2",
   "metadata": {
    "jupyter": {
     "source_hidden": true
    }
   },
   "outputs": [],
   "source": [
    "%%idrrun -a\n",
    "module calcul\n",
    "    use iso_fortran_env, only : INT32, REAL64\n",
    "    use openacc    \n",
    "    contains\n",
    "        subroutine rand_init(array,n)\n",
    "            real   (kind=REAL64), dimension(1,n), intent(inout) :: array\n",
    "            integer(kind=INT32 ), intent(in)                    :: n\n",
    "            real   (kind=REAL64)                                :: rand_val\n",
    "            integer(kind=INT32)                                 :: i\n",
    "\n",
    "            call srand(12345900)\n",
    "            do i = 1, n\n",
    "               call random_number(rand_val)\n",
    "               array(1,i) = 2.0_real64*(rand_val-0.5_real64)\n",
    "            enddo\n",
    "        end subroutine rand_init\n",
    "\n",
    "        subroutine iterate(array, array_size, cell_size)\n",
    "        !$acc routine worker\n",
    "            real   (kind=REAL64), dimension(1:array_size,1), intent(inout) :: array\n",
    "            integer(kind=INT32 ), intent(in)                               :: array_size, cell_size\n",
    "            real   (kind=REAL64)                                           :: local_mean\n",
    "            integer(kind=INT32 )                                           :: i\n",
    " \n",
    "            !$acc loop seq\n",
    "            do i = cell_size/2, array_size-cell_size/2\n",
    "                local_mean = mean_value(array(i+1-cell_size/2:i+cell_size/2,1), cell_size)\n",
    "                if (local_mean .lt. 0.0_real64) then\n",
    "                    array(i,1) = array(i,1) + 0.1\n",
    "                else\n",
    "                    array(i,1) = array(i,1) - 0.1\n",
    "                endif\n",
    "            enddo\n",
    "        end subroutine iterate\n",
    "\n",
    "        function mean_value(t, n)\n",
    "        !$acc routine vector\n",
    "            real   (kind=REAL64), dimension(n,1), intent(inout) :: t\n",
    "            integer(kind=INT32 ), intent(in)                    :: n\n",
    "            real   (kind=REAL64)                                :: mean_value\n",
    "            integer(kind=INT32 )                                :: i\n",
    "            mean_value = 0.0_real64\n",
    "            !$acc loop reduction(+:mean_value)\n",
    "            do i = 1, n\n",
    "                mean_value = mean_value + t(i,1)\n",
    "            enddo\n",
    "            mean_value = mean_value / dble(n)\n",
    "        end function mean_value\n",
    "end module calcul\n",
    "program modular_programming\n",
    "    use calcul\n",
    "    implicit none    \n",
    "    \n",
    "    real   (kind=REAL64), dimension(:,:), allocatable :: table\n",
    "    real   (kind=REAL64), dimension(:)  , allocatable :: mean_values\n",
    "    integer(kind=INT32 )                              :: nx, ny, cell_size, i\n",
    "\n",
    "    nx =   10000\n",
    "    ny =    3000\n",
    "    allocate(table(nx,ny), mean_values(ny))\n",
    "    table(:,:) = 0.0_real64\n",
    "    call rand_init(table(1,:),ny)\n",
    "    !$acc enter data copyin(table(:,:))\n",
    "    cell_size = 32\n",
    "    !$acc parallel loop\n",
    "    do i = 2, ny   \n",
    "        call iterate(table(:,i), nx, cell_size)\n",
    "    enddo\n",
    "\n",
    "    !$acc parallel loop gang present(table(:,:)) copyout(mean_values(:))\n",
    "    do i = 1, ny\n",
    "        mean_values(i) = mean_value(table(:,i), nx)\n",
    "    enddo\n",
    "\n",
    "    do i = 1, 10\n",
    "        write(0,\"(a18,i5,a1,f20.8)\") \"Mean value of row \",i,\"=\",mean_values(i)\n",
    "    enddo\n",
    "\n",
    "    do i = ny-10, ny\n",
    "        write(0,\"(a18,i5,a1,f20.8)\") \"Mean value of row \",i,\"=\",mean_values(i)\n",
    "    enddo    \n",
    "    \n",
    "    !$acc exit data delete(table)\n",
    "    deallocate(table, mean_values)\n",
    "end program modular_programming"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "GPU Directives",
   "language": "python",
   "name": "gpu_directives"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
