{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "a6e1507b",
   "metadata": {
    "editable": false
   },
   "source": [
    "# Advanced loop configuration\n",
    "\n",
    "---\n",
    "**Requirements:**\n",
    "\n",
    "- [Get started](./Get_started.ipynb)\n",
    "- [Variables_status](./Variables_status.ipynb)\n",
    "- [Data management](./Data_management.ipynb)\n",
    "\n",
    "---\n",
    "\n",
    "Different levels of parallelism are generated by the _gang_, _worker_ and _vector_ clauses.\n",
    "The _loop_ directive is responsible for sharing the parallelism across the different levels.\n",
    "\n",
    "The degree of parallelism in a given level is determined by the numbers of gangs, workers and threads. These numbers are defined by the implementation. This default behavior depends on not only the target architecture but also on the portion of code on which the parallelism is applied.\n",
    "No modifications of this default behavior is recommended as it presents good optimization.\n",
    "\n",
    "It is however possible to specify the numbers of gangs, workers and threads in the parallel construct with the _num_gangs_, _num_workers_ and _vector_length_ clauses. These clauses are allowed with the _parallel_ and _kernel_ construct. You might want to use these clauses in order to:\n",
    "\n",
    "- debug (to restrict the execution on a single gang (without restrictions on the vectors as the _serial_ clause will do), to vary the parallelism degree in order to expose a race condition ...)\n",
    "- limit the number of gang to lower the memory occupancy when you have to privatize arrays"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "091a749e",
   "metadata": {
    "editable": false
   },
   "source": [
    "## Syntax\n",
    "\n",
    "Clauses to specify the numbers of gangs, workers and vectors are _num_gangs_, _num_workers_ and _vector_length_.\n",
    "\n",
    "It is possible to use either numbers or variables as arguments of these clauses. The numbers must be positive integers and the variable should refer to a scalar integer variable.\n",
    "\n",
    "```fortran\n",
    "!$acc parallel num_gangs(3500) vector_length(128)\n",
    "!$acc loop gang\n",
    "do j = 1, size_j\n",
    "    !$acc loop vector\n",
    "    do i = 1, size_i\n",
    "        ! A fabulous calculation\n",
    "    enddo\n",
    "enddo\n",
    "!$acc end parallel\n",
    "\n",
    "!$acc parallel loop gang num_gangs(size_j/2) vector_length(128)\n",
    "do j = 1, size_j\n",
    "    !$acc loop vector\n",
    "    do i = 1, size_i\n",
    "        ! A fabulous calculation\n",
    "    enddo\n",
    "enddo\n",
    "!$acc end parallel\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "bc3a43f3",
   "metadata": {
    "editable": false
   },
   "source": [
    "## Restrictions\n",
    "\n",
    "The restrictions described here are for NVIDIA architectures.\n",
    "\n",
    "- The number of gang is limited to 2³¹-1 (65535 if the compute capability is lower than 3.0).\n",
    "- The product num_workers x vector_length can not be higher than 1024 (512 if the compute capability is lower than 2.0).\n",
    "- To achieve performances, it is better to set the vector_length as a multiple of 32 (up to 1024).\n",
    "- Using routines with a _vector_ level of parallelization or higher sets the _vector_length_ to 32 (compiler limitation).\n",
    "\n",
    "This restrictions can vary with the architecture and it is advised to refer to the \"Cuda C programming Guide\" (Section G \"Features and Technical Specifications\") for future implementations."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "984ef006",
   "metadata": {
    "editable": false
   },
   "source": [
    "## Example\n",
    "\n",
    "Example stored in: `../../examples/Fortran/Loop_configuration_example.f90`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ce97f678",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%idrrun -a\n",
    "program loop_configuration\n",
    "    use ISO_FORTRAN_ENV, only : INT32, REAL64\n",
    "    use openacc\n",
    "    implicit none    \n",
    "    integer(kind=INT32), parameter        :: n = 500\n",
    "    integer(kind=INT32), dimension(n,n,n) :: table\n",
    "    integer(kind=INT32)                   :: ngangs, nworkers, nvectors    \n",
    "    integer(kind=INT32)                   :: i, j, k\n",
    "\n",
    "    ngangs   = 450\n",
    "    nworkers = 4\n",
    "    nvectors = 16 \n",
    "\n",
    "    !$acc parallel loop gang num_gangs(ngangs) num_workers(nworkers) vector_length(nvectors) copyout(table(:,:,:))\n",
    "    do k = 1, n\n",
    "        !$acc loop worker\n",
    "        do j = 1, n\n",
    "            !$acc loop vector\n",
    "            do i = 1, n\n",
    "                table(i,j,k) = i + j*1000 + k*1000*1000\n",
    "            enddo\n",
    "        enddo\n",
    "    enddo\n",
    "\n",
    "    print *, table(1,1,1), table(n,n,n)\n",
    "end program loop_configuration      "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d78ff930",
   "metadata": {
    "editable": false
   },
   "source": [
    "## Exercise\n",
    "\n",
    "A simple exercise can be to modify the value of the _num_gang_ clause (and add a variation to the vector length) and then compare the execution time.\n",
    "\n",
    "For a change, we will make an exercise that don't make sense physically. It can however come handy, especially if you try the practical work on HYDRO. In this exercise, you will have to:\n",
    "\n",
    "- parallelize a few lines of codes\n",
    "- be sure that it reproduces well the CPU behavior\n",
    "- manually modify the number of gangs\n",
    "- observe that the number of gangs will be limited by the system's size in this code\n",
    "\n",
    "Example stored in: `../../examples/Fortran/Loop_configuration_exercise.f90`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "009d2c74",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%idrrun --cliopts \"500\"\n",
    "program Loop_configuration\n",
    "    use ISO_FORTRAN_ENV, only : INT32, REAL64, INT64\n",
    "    implicit none\n",
    "    integer(kind=INT32 ), parameter              :: system_size = 50000\n",
    "    real   (kind=REAL64), dimension(system_size) :: array\n",
    "    real   (kind=REAL64), dimension(system_size) :: table\n",
    "    real   (kind=REAL64)                         :: sum_val, res, norm, time\n",
    "    integer(kind=INT32 )                         :: i, j, length, ngangs, numarg\n",
    "    character(len=:)    , allocatable            :: arg1\n",
    "\n",
    "    numarg = command_argument_count()\n",
    "    if (numarg .ne. 1) then\n",
    "        write(0,*) \"Error, you should provide an argument of integer kind to specify the number of gangs that will be used\"\n",
    "        stop\n",
    "    endif\n",
    "    call get_command_argument(1,LENGTH=length)\n",
    "    allocate(character(len=length) :: arg1)\n",
    "    call get_command_argument(1,VALUE=arg1)\n",
    "    read(arg1,'(i10)') ngangs\n",
    "\n",
    "    norm    = 1.0_real64 / (int(system_size, INT64) * int(system_size, INT64))\n",
    "    res     = 0.0_real64 ! to compare CPU and GPU quickly\n",
    "\n",
    "    do j = 1, system_size\n",
    "        sum_val = 0.0_real64\n",
    "        do i = 1, system_size\n",
    "            table(i) = (i+j) * norm\n",
    "        enddo\n",
    "\n",
    "        do i = 1, system_size\n",
    "            sum_val  = sum_val + table(i)\n",
    "        enddo\n",
    "        array(j) = sum_val\n",
    "        res = res + sum_val\n",
    "    enddo\n",
    "\n",
    "    print *, \"result: \",res\n",
    "\n",
    "!    do i = 1, system_size\n",
    "!        print *, i,array(i)\n",
    "!    enddo\n",
    "\n",
    "end program Loop_configuration"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c86d34ef",
   "metadata": {
    "editable": false
   },
   "source": [
    "### Solution\n",
    "\n",
    "Example stored in: `../../examples/Fortran/Loop_configuration_solution.f90`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "22d5c5cb",
   "metadata": {
    "jupyter": {
     "source_hidden": true
    }
   },
   "outputs": [],
   "source": [
    "%%idrrun -a --cliopts \"500\"\n",
    "program Loop_configuration\n",
    "    use ISO_FORTRAN_ENV, only : INT32, REAL64, INT64\n",
    "    implicit none\n",
    "    integer(kind=INT32 ), parameter              :: system_size = 50000\n",
    "    real   (kind=REAL64), dimension(system_size) :: array\n",
    "    real   (kind=REAL64), dimension(system_size) :: table\n",
    "    real   (kind=REAL64)                         :: sum_val, res, norm, time\n",
    "    integer(kind=INT32 )                         :: i, j, length, ngangs, numarg\n",
    "    character(len=:)    , allocatable            :: arg1\n",
    "\n",
    "    numarg = command_argument_count()\n",
    "    if (numarg .ne. 1) then\n",
    "        write(0,*) \"Error, you should provide an argument of integer kind to specify the number of gangs that will be used\"\n",
    "        stop\n",
    "    endif\n",
    "    call get_command_argument(1,LENGTH=length)\n",
    "    allocate(character(len=length) :: arg1)\n",
    "    call get_command_argument(1,VALUE=arg1)\n",
    "    read(arg1,'(i10)') ngangs\n",
    "\n",
    "    norm    = 1.0_real64 / (int(system_size, INT64) * int(system_size, INT64))    \n",
    "    res     = 0.0_real64 ! to compare CPU and GPU quickly\n",
    "\n",
    "    !$acc parallel num_gangs(ngangs) copyout(array(:)) private(table(:))\n",
    "    !$acc loop gang reduction(+:res)\n",
    "    do j = 1, system_size\n",
    "        sum_val = 0.0_real64\n",
    "        !$acc loop vector\n",
    "        do i = 1, system_size\n",
    "            table(i) = (i+j) * norm\n",
    "        enddo\n",
    "\n",
    "        !$acc loop vector reduction(+:sum_val)\n",
    "        do i = 1, system_size\n",
    "            sum_val  = sum_val + table(i)\n",
    "        enddo\n",
    "        array(j) = sum_val\n",
    "        res = res + sum_val\n",
    "    enddo\n",
    "    !$acc end parallel\n",
    "\n",
    "    print *, \"result: \",res\n",
    "\n",
    "!    do i = 1, system_size\n",
    "!        print *, i,array(i)  \n",
    "!    enddo\n",
    "\n",
    "end program Loop_configuration"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "GPU Directives",
   "language": "python",
   "name": "gpu_directives"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
