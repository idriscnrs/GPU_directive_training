{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "4dc68b60",
   "metadata": {
    "editable": false
   },
   "source": [
    "# Hands-on Game Of Life\n",
    "\n",
    "---\n",
    "**Requirements:**\n",
    "\n",
    "- [Get started](./Get_started.ipynb)\n",
    "- [Data management](./Data_management.ipynb)\n",
    "\n",
    "---\n",
    "\n",
    "The Game of Life is a cellular automaton developed by John Conway in 1970. In this \"Game\" a grid is filled with an __initial state__ of cells having either the status \"dead\" or \"alive\". From this initial state, several generations are computed and we can follow the evolution of the cells for each __generation__.\n",
    "\n",
    "The rules are simple:\n",
    "\n",
    "- For \"dead\" cells:\n",
    "  - If it has exactly 3 neighbors the cell becomes __alive__ at the next generation\n",
    "  \n",
    "  <img alt=\"Cell birth\" src=\"../../pictures/naissance.png\" style=\"float:none\"/>\n",
    "\n",
    "- For \"alive\" cells:\n",
    "  - If it has more than 3 neighbors the cell becomes __dead__ because of overpopulation \n",
    "  \n",
    "  <img alt=\"Cell death overpopulation\" src=\"../../pictures/mort_sur_pop.png\" style=\"float:none\"/>\n",
    "  \n",
    "  - If it has less than 2 neighbors the cell becomes __dead__ because of underpopulation \n",
    "  \n",
    "  <img alt=\"Cell death underpopulation\" src=\"../../pictures/mort_sous_pop.png\" style=\"float:none\"/>\n",
    "\n",
    "For all other situations the state of the cell is kept unchanged. \n",
    "\n",
    "<img alt=\"No change\" src=\"../../pictures/no_change.png\" style=\"float:none\"/>"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "88a74f9b",
   "metadata": {
    "editable": false
   },
   "source": [
    "## What to do\n",
    "\n",
    "In this hands-on you have to add the directives to perform the following actions:\n",
    "\n",
    "- Copy the initial state of the world generated on the CPU to the GPU\n",
    "- Make sure that the computation of the current generation and the saving of the previous one occur on the GPU\n",
    "- Compute the number of cells alive for the current generation is done on the GPU\n",
    "- The memory on the GPU is allocated and freed when the arrays are not needed anymore\n",
    "\n",
    "Example stored in: `../../examples/Fortran/GameOfLife_exercise.f90`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9fb3e4eb",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%idrrun  -a --cliopts \"20000 1000 300\" \n",
    "program gol\n",
    "    implicit none\n",
    "    integer :: rows, cols, generations\n",
    "    integer, allocatable :: world(:,:), old_world(:,:)\n",
    "    integer :: g\n",
    "    integer :: long\n",
    "    character(len=:), allocatable :: arg\n",
    "    integer :: i\n",
    "    integer, dimension(3) :: n\n",
    "    ! Read cmd line args\n",
    "    DO i=1, COMMAND_ARGUMENT_COUNT()\n",
    "       CALL GET_COMMAND_ARGUMENT(NUMBER=i, LENGTH=long)\n",
    "       ALLOCATE(CHARACTER(len=long) :: arg)\n",
    "       CALL GET_COMMAND_ARGUMENT(NUMBER=i, VALUE=arg)\n",
    "       READ(arg,'(i10)') n(i)\n",
    "       DEALLOCATE(arg)\n",
    "    END DO\n",
    "    rows=n(1)\n",
    "    cols=n(2)\n",
    "    generations=n(3)\n",
    "    allocate(world(0:rows+1,0:cols+1), old_world(0:rows+1,0:cols+1))\n",
    "    old_world(:,:) = 0\n",
    "    call fill_world\n",
    "    do g=1,generations\n",
    "        call save_world(rows,cols, world(:,:), old_world(:,:))\n",
    "        call next(rows,cols, world(:,:), old_world(:,:))\n",
    "        print *, \"Cells alive at generation \", g, \": \", alive(rows, cols, world)\n",
    "        call output_world(\"generation\", g, world(:,:), rows, cols)\n",
    "    enddo\n",
    "    deallocate(world, old_world)\n",
    "    contains\n",
    "    integer function alive(rows, cols, world) result(cells)\n",
    "        implicit none\n",
    "        integer, intent(in) :: rows, cols\n",
    "        integer, intent(in) :: world(0:rows+1,0:cols+1)\n",
    "        integer :: r,c\n",
    "        cells = 0\n",
    "        do r=1, rows\n",
    "            do c=1, cols\n",
    "               cells = cells + world(r,c) \n",
    "            enddo\n",
    "        enddo\n",
    "        end function alive\n",
    "    subroutine save_world(rows,cols,world, old_world)\n",
    "        implicit none\n",
    "        integer, intent(in) :: rows,cols\n",
    "        integer, intent(in) :: world(0:rows+1,0:cols+1)\n",
    "        integer, intent(out) :: old_world(0:rows+1,0:cols+1)\n",
    "        integer :: r,c\n",
    "        do r=1, rows\n",
    "            do c=1, cols\n",
    "                old_world(r,c) = world(r,c)\n",
    "            enddo\n",
    "        enddo\n",
    "    end subroutine save_world\n",
    "\n",
    "    subroutine next(rows,cols,world, old_world)\n",
    "        implicit none\n",
    "        integer, intent(in) :: rows,cols\n",
    "        integer, intent(in) :: old_world(0:rows+1,0:cols+1)\n",
    "        integer, intent(out) :: world(0:rows+1,0:cols+1)\n",
    "        integer :: r,c\n",
    "        integer :: neigh\n",
    "        do r=1, rows\n",
    "            do c=1, cols\n",
    "        neigh = old_world(r-1,c-1)+old_world(r,c-1)+old_world(r+1,c-1)+&\n",
    "                old_world(r-1,c)+old_world(r+1,c)+&\n",
    "                old_world(r-1,c+1)+old_world(r,c+1)+old_world(r+1,c+1)\n",
    "                if (old_world(r,c) == 1 .and. (neigh<2.or.neigh>3) ) then\n",
    "                    world(r,c) = 0\n",
    "                else if (neigh == 3) then\n",
    "                    world(r,c) = 1\n",
    "                endif\n",
    "            enddo\n",
    "        enddo\n",
    "    end subroutine next\n",
    "\n",
    "    subroutine fill_world\n",
    "        implicit none\n",
    "        integer :: r,c, temp\n",
    "        real*8 :: test\n",
    "        do r=1,rows\n",
    "            do c=1,cols\n",
    "                call random_number(test)\n",
    "                temp = mod(floor(test*100),4)\n",
    "                if (temp.eq.0) then \n",
    "                    temp = 1\n",
    "                else\n",
    "                    temp = 0\n",
    "                endif\n",
    "                world(r,c) = temp\n",
    "            enddo\n",
    "        enddo\n",
    "    end subroutine fill_world\n",
    "    \n",
    "    subroutine output_world(name, g, world, rows, cols)\n",
    "        implicit none\n",
    "        character(len=*), intent(in) :: name\n",
    "        character(len=1024) :: filename\n",
    "        integer, intent(in) :: g\n",
    "        integer, intent(in) :: rows,cols\n",
    "        integer, intent(in) :: world(0:rows+1,0:cols+1)\n",
    "        integer :: r,c\n",
    "        integer :: my_unit\n",
    "        integer(kind=1) :: output(0:rows+1,0:cols+1)\n",
    "        write(filename,\"(A,I5.5,A)\") trim(name), g,\".gray\"\n",
    "        print *, trim(filename)\n",
    "        do r=0,rows+1\n",
    "            do c=0,cols+1\n",
    "                output(r,c) = mod(world(r,c),2)*127\n",
    "            enddo\n",
    "        enddo\n",
    "        open(newunit=my_unit, file=filename, access=\"stream\", form=\"unformatted\")\n",
    "        write(my_unit, pos=1) output\n",
    "        close(my_unit)\n",
    "    end subroutine output_world\n",
    "end program gol"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "07daf7f4",
   "metadata": {},
   "outputs": [],
   "source": [
    "rows = 2000\n",
    "cols = 1000\n",
    "\n",
    "from idrcomp import convert_pic\n",
    "import matplotlib.pyplot as plt\n",
    "import glob\n",
    "from PIL import Image\n",
    "from ipywidgets import interact\n",
    "\n",
    "files = sorted(glob.glob(\"*.gray\"))\n",
    "images = [convert_pic(f, cols, rows, \"L\") for f in files]\n",
    "print(images[0].size)\n",
    "def view(i):\n",
    "    crop = (155,65,360,270)\n",
    "    plt.figure(figsize=(12,12))\n",
    "    plt.imshow(images[i].crop(crop), cmap=\"Greys\")\n",
    "    plt.show()\n",
    "interact(view, i=(0, len(images)-1))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8096ddcc",
   "metadata": {
    "editable": false
   },
   "source": [
    "## Solution\n",
    "\n",
    "Example stored in: `../../examples/Fortran/GameOfLife_solution.f90`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e44630f5",
   "metadata": {
    "jupyter": {
     "source_hidden": true
    }
   },
   "outputs": [],
   "source": [
    "%%idrrun  -a --cliopts \"20000 1000 300\" \n",
    "program gol\n",
    "    implicit none\n",
    "    integer :: rows, cols, generations\n",
    "    integer, allocatable :: world(:,:), old_world(:,:)\n",
    "    integer :: g\n",
    "    integer :: long\n",
    "    character(len=:), allocatable :: arg\n",
    "    integer :: i\n",
    "    integer, dimension(3) :: n\n",
    "    ! Read cmd line args\n",
    "    DO i=1, COMMAND_ARGUMENT_COUNT()\n",
    "       CALL GET_COMMAND_ARGUMENT(NUMBER=i, LENGTH=long)\n",
    "       ALLOCATE(CHARACTER(len=long) :: arg)\n",
    "       CALL GET_COMMAND_ARGUMENT(NUMBER=i, VALUE=arg)\n",
    "       READ(arg,'(i10)') n(i)\n",
    "       DEALLOCATE(arg)\n",
    "    END DO\n",
    "    rows=n(1)\n",
    "    cols=n(2)\n",
    "    generations=n(3)\n",
    "    allocate(world(0:rows+1,0:cols+1), old_world(0:rows+1,0:cols+1))\n",
    "    old_world(:,:) = 0\n",
    "    call fill_world\n",
    "    !$ACC enter data copyin(world, old_world)\n",
    "    do g=1,generations\n",
    "        call save_world(rows,cols, world(:,:), old_world(:,:))\n",
    "        call next(rows,cols, world(:,:), old_world(:,:))\n",
    "        print *, \"Cells alive at generation \", g, \": \", alive(rows, cols, world)\n",
    "        call output_world(\"generation\", g, world(:,:), rows, cols)\n",
    "    enddo\n",
    "    !$ACC exit data delete(world, old_world)\n",
    "    deallocate(world, old_world)\n",
    "    contains\n",
    "    integer function alive(rows, cols, world) result(cells)\n",
    "        implicit none\n",
    "        integer, intent(in) :: rows, cols\n",
    "        integer, intent(in) :: world(0:rows+1,0:cols+1)\n",
    "        integer :: r,c\n",
    "        cells = 0\n",
    "        !$ACC parallel loop reduction(+:cells)\n",
    "        do r=1, rows\n",
    "            do c=1, cols\n",
    "               cells = cells + world(r,c) \n",
    "            enddo\n",
    "        enddo\n",
    "        end function alive\n",
    "    subroutine save_world(rows,cols,world, old_world)\n",
    "        implicit none\n",
    "        integer, intent(in) :: rows,cols\n",
    "        integer, intent(in) :: world(0:rows+1,0:cols+1)\n",
    "        integer, intent(out) :: old_world(0:rows+1,0:cols+1)\n",
    "        integer :: r,c\n",
    "        !$ACC parallel loop\n",
    "        do r=1, rows\n",
    "            do c=1, cols\n",
    "                old_world(r,c) = world(r,c)\n",
    "            enddo\n",
    "        enddo\n",
    "    end subroutine save_world\n",
    "\n",
    "    subroutine next(rows,cols,world, old_world)\n",
    "        implicit none\n",
    "        integer, intent(in) :: rows,cols\n",
    "        integer, intent(in) :: old_world(0:rows+1,0:cols+1)\n",
    "        integer, intent(out) :: world(0:rows+1,0:cols+1)\n",
    "        integer :: r,c\n",
    "        integer :: neigh\n",
    "        !$ACC parallel loop\n",
    "        do r=1, rows\n",
    "            do c=1, cols\n",
    "        neigh = old_world(r-1,c-1)+old_world(r,c-1)+old_world(r+1,c-1)+&\n",
    "                old_world(r-1,c)+old_world(r+1,c)+&\n",
    "                old_world(r-1,c+1)+old_world(r,c+1)+old_world(r+1,c+1)\n",
    "                if (old_world(r,c) == 1 .and. (neigh<2.or.neigh>3) ) then\n",
    "                    world(r,c) = 0\n",
    "                else if (neigh == 3) then\n",
    "                    world(r,c) = 1\n",
    "                endif\n",
    "            enddo\n",
    "        enddo\n",
    "    end subroutine next\n",
    "\n",
    "    subroutine fill_world\n",
    "        implicit none\n",
    "        integer :: r,c, temp\n",
    "        real*8 :: test\n",
    "        do r=1,rows\n",
    "            do c=1,cols\n",
    "                call random_number(test)\n",
    "                temp = mod(floor(test*100),4)\n",
    "                if (temp.eq.0) then \n",
    "                    temp = 1\n",
    "                else\n",
    "                    temp = 0\n",
    "                endif\n",
    "                world(r,c) = temp\n",
    "            enddo\n",
    "        enddo\n",
    "    end subroutine fill_world\n",
    "    \n",
    "    subroutine output_world(name, g, world, rows, cols)\n",
    "        implicit none\n",
    "        character(len=*), intent(in) :: name\n",
    "        character(len=1024) :: filename\n",
    "        integer, intent(in) :: g\n",
    "        integer, intent(in) :: rows,cols\n",
    "        integer, intent(in) :: world(0:rows+1,0:cols+1)\n",
    "        integer :: r,c\n",
    "        integer :: my_unit\n",
    "        integer(kind=1) :: output(0:rows+1,0:cols+1)\n",
    "        write(filename,\"(A,I5.5,A)\") trim(name), g,\".gray\"\n",
    "        print *, trim(filename)\n",
    "        !$ACC update self(world)\n",
    "        do r=0,rows+1\n",
    "            do c=0,cols+1\n",
    "                output(r,c) = mod(world(r,c),2)*127\n",
    "            enddo\n",
    "        enddo\n",
    "        open(newunit=my_unit, file=filename, access=\"stream\", form=\"unformatted\")\n",
    "        write(my_unit, pos=1) output\n",
    "        close(my_unit)\n",
    "    end subroutine output_world\n",
    "end program gol"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1d3113b3",
   "metadata": {},
   "outputs": [],
   "source": [
    "rows = 2000\n",
    "cols = 1000\n",
    "\n",
    "from idrcomp import convert_pic\n",
    "import matplotlib.pyplot as plt\n",
    "import glob\n",
    "from PIL import Image\n",
    "from ipywidgets import interact\n",
    "\n",
    "files = sorted(glob.glob(\"*.gray\"))\n",
    "images = [convert_pic(f, cols, rows, \"L\") for f in files]\n",
    "print(images[0].size)\n",
    "def view(i):\n",
    "    crop = (155,65,360,270)\n",
    "    plt.figure(figsize=(12,12))\n",
    "    plt.imshow(images[i].crop(crop), cmap=\"Greys\")\n",
    "    plt.show()\n",
    "interact(view, i=(0, len(images)-1))"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "GPU Directives",
   "language": "python",
   "name": "gpu_directives"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
